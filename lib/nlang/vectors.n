from nlang import *
import nlang.unsafe
from nlang.sysheap import heap sysheap

export inline type vector t:i_copyable = \
    export i_trivial_ctor \
    export i_copyable
  _p:(slice ut)
  _count:size
  alias ut = (nlang.unsafe.unconstructed t)

export vector method count = size
  return self._count

export vector method$ at n:size = @$t
  assert (n <= self._count)
  return @$(self._p$at n)$value

export vector method$ first = @$t
  return self$at 0

export vector method$ last = @$t
  return self$at self._count-1

export vector method is_empty = bool
  return self._count == 0

export vector method capacity = size
  return self._p.count

export vector method# reserve n:size = void
  if n > self.capacity
    (heap.slice_realloc ut) @!sysheap @#self#_p n

export vector method# resize n:size = void
  (heap.slice_realloc ut) @!sysheap @#self#_p n
  self!_count = n

export vector method# push x:t = void
  self#reserve self._count+1
  let n = self._count
  self!_count += 1
  let d = self!at n
  d! = x

export vector method# pop = t
  assert (self._count != 0)
  let d = self.last
  self!_count -= 1
  return d.

export vector method# clear = void
  (heap.slice_free ut) @!sysheap @#self#_p

export vector method all = index_range
  return range 0 self.count-1

export vector method# i_copyable.copy_ctor other:@this = void
  (heap.slice_realloc ut) @!sysheap @#self#_p other._p.count
  self._p!copy 0 @other._p 0 other._p.count

export vector method# copy_slice other:@(slice ut) = void
  (heap.slice_realloc ut) @!sysheap @#self#_p other.count
  self._p!copy 0 other 0 other.count
  self!_count = other.count
