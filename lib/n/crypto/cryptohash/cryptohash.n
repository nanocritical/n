from n import *
import openssl
import n.fmt
import n.io
import n.encoding.hex

enum Function =
	| SHA256 = 0
	| SHA224
	| SHA384
	| SHA512
	| SHA1
	| MD5

Function met Bytecount = Uint
	match self
	| SHA256 -> return 256/8
	| SHA224 -> return 224/8
	| SHA384 -> return 384/8
	| SHA512 -> return 512/8
	| SHA1 -> return 160/8
	| MD5 -> return 128/8

Function met openssl_evp_md = *openssl.EVP_MD
	match self
	| SHA256 -> return openssl.EVP_sha256
	| SHA224 -> return openssl.EVP_sha224
	| SHA384 -> return openssl.EVP_sha384
	| SHA512 -> return openssl.EVP_sha512
	| SHA1 -> return openssl.EVP_sha1
	| MD5 -> return openssl.EVP_md5


struct Digest =
	isa `Show
	Function	:Function
	Binary		:Stringbuf

Digest met Show st:#fmt.`State
	fmt.F st "%s:%s" self.Hex self.Function

Digest met Hex = r:Stringbuf
	r#Resize <- hex.Encoded_length self.Function.Bytecount
	never, _ = hex.Encode r!Bytes self.Binary.Bytes


-- Convenience function.
fun Hash function:Function b:[]U8 = md:Digest
	var state:State
	state#Init function
	_, never = state#Write b
	md = state#Final


struct State =
	isa io.`Writer
	ctx		:openssl.EVP_MD_CTX
	ctx_initialized	:Bool
	function	:Function

State met# Dtor
	never = openssl.EVP_MD_CTX_cleanup #self#ctx

State met# Init function:Function
	if not self.ctx_initialized
		openssl.EVP_MD_CTX_init #self#ctx
		self#ctx_initialized = true

	self#function = function
	never = openssl.EVP_DigestInit_ex #self#ctx self.function.openssl_evp_md nil

State met# Write b:[]U8 = empty:[]U8, Error
	assert self.ctx_initialized
	never = openssl.EVP_DigestUpdate #self#ctx b

State met# Final = digest:Digest
	digest#Function = self.function
	digest#Binary#Resize self.function.Bytecount
	never, _ = openssl.EVP_DigestFinal_ex #self#ctx digest!Binary!Bytes![.. 0]


example sha1
	let msg = "def"

	var state:State
	state#Init SHA1
	_, never = state#Write msg.Bytes
	let md = state#Final
	assert md.Function == SHA1
	assert md.Hex == "589c22335a381f122d129225f5c0ba3056ed5811"

	var buf:Stringbuf
	fmt.F #buf "%s" md
	assert buf == "589c22335a381f122d129225f5c0ba3056ed5811:SHA1"

	let md2 = Hash SHA1 msg.Bytes
	assert md2.Function == SHA1
	assert md2.Hex == "589c22335a381f122d129225f5c0ba3056ed5811"

	let md3 = Hash MD5 msg.Bytes
	assert md3.Function == MD5
	assert md3.Hex == "4ed9407630eb1000c0f6b63842defa7d"

	let md4 = Hash SHA256 msg.Bytes
	assert md4.Function == SHA256
	assert md4.Hex == "cb8379ac2098aa165029e3938a51da0bcecfc008fd6795f401178647f96c5b34"
