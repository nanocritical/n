from n import *
import n
import n.io

---- TODO(e): Should return IO errors.
-- An alternate, easy-going library "dbg" should be created with no error
-- return.

intf `State =
	isa io.`Writer
	met Width = ?Uint
	met Precision = ?Uint
	met Flag c:Rune = Bool

inline struct State =
	isa `State
	W	:#io.`Writer

State met# Init w:#io.`Writer
	self#W = w

State met Width = ?Uint
	return nil

State met Precision = ?Uint
	return nil

State met Flag c:Rune = Bool
	return false

State met# Write b:[]U8 = []U8, Error
	return self#W#Write b

fun first_verb s:String = before:String, verb:String, after:String
	let i = s.Index_byte "%"
	if i == -1
		return s, "", ""

	before, after = s.Cut_at i.Unsigned
	if after.Count < 2
		return s, "", ""

	return before, after.[.. 2], after.[2 ..]

fun F w:#io.`Writer fmt:String ...v:(Vararg ?*`Show)
	let st = {W=w}:State
	try
		while true
			let before, verb, after = first_verb fmt
			_, except = io.Write_full w before.Bytes
			if verb.Count == 0
				break
			if verb.[1] == "%"
				_, except = io.Write_full w "%".Bytes
			elif verb.[1] == "s"
				let s = v!Next
				if not s?
					break
				s.Show #st
			fmt = after
	catch _
		noop

fun Prf fmt:String ?err:?Bool ...v:(Vararg ?*`Show)
	var buf = #{}:Stringbuf
	F buf fmt ...v
	n.Pr err=err buf

fun Pr ?err:?Bool ?sep:?*String  ...v:(Vararg ?*`Show)
	var buf = #{}:Stringbuf
	Cat buf sep=sep ...v
	n.Pr err=err buf

fun Prn ?err:?Bool ?sep:?*String ...v:(Vararg ?*`Show)
	var buf = #{}:Stringbuf
	Cat buf sep=sep ...v
	n.Prn err=err buf

fun Cat w:#io.`Writer ?sep:?*String ...v:(Vararg *`Show)
	let st = {W=w}:State
	try
		for s in v
			s.Show #st
			if sep? and v.Count_left > 0
				_, except = io.Write_full w sep.Bytes
	catch _
		noop

fun Sf fmt:String ...v:(Vararg *`Show) = r:Stringbuf
	F #r fmt ...v

fun S ?sep:?*String ...v:(Vararg *`Show) = r:Stringbuf
	Cat #r sep=(sep else " ") ...v
