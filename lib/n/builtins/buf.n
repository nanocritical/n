inline struct Buf t:{`Moveable `Trivial_ctor `Default_dtor} =
	isa `Trivial_ctor `Copyable (`Random_container t) `Show
	s	:[!]t	--::^Owned

inline Buf fun From_slice c:[]t = r:this
	r#Resize c.Count
	r#Slice!Exact_copy c

inline Buf met# Dtor
	if t isa `Trivial_dtor
		noop
	else
		foreach i in self.All
			self![i]#Dtor
	Realloc #self#s 0

inline Buf met# Copy_ctor other:*this
	self#Copy other.Slice

inline Buf met Cap = Uint
	return self.s.cap

inline Buf met Count = Uint
	return self.s.cnt

inline Buf met All = Range
	return self.s.All

inline Buf met$ Slice = [$]t
	return self$s

inline Buf met# Zero
	assert t isa `Trivial_ctor
	let raw = self!s!Unsafe_rawdata
	and bytecnt = self.Count * (sizeof t)
	Slice_memset raw 0 bytecnt

inline Buf met# Clear
	self!s = self.s.[.. 0]
	self#auto_shrink

inline Buf met# Resize cnt:Uint
	if cnt <= self.Cap
		self!s = self.s.[.. cnt]
		self#auto_shrink
	else
		self#Reserve cnt
		self!s = self.s.[.. cnt]

inline Buf met# Push = #t
	let nend = self.s.Count + 1
	self#auto_grow nend
	self!s = self!s![.. nend]
	return self#Last

inline Buf shallow met$ First = $t
	return self$s$First

inline Buf shallow met$ Last = $t
	return self$s$Last

inline Buf shallow met$ Operator_at n:Uint = r:$t
	return self$s$Operator_at n

inline Buf shallow met$ Operator_sub r:Range = [$]t
	return self$s$Operator_sub r

inline Buf met# Pop
	assert self.s.Count > 0
	self!s = self.s.[.. self.s.Count-1]
	self#auto_shrink

inline Buf met# Append s:[]t
	assert t isa `Copyable
	self#auto_grow self.s.Count + s.Count
	self#s = self!s!Append s

inline Buf met# Copy s:[]t
	assert t isa `Copyable
	self#Reserve s.Count
	self#s = self.s!Copy s
	self#auto_shrink

-- The automatic growth of Buf et al* slows down when the buffer is large.
fun growth_factor_shift n:Uint = Uint
	if n <= 256
		return 0
	elif n <= 16_384
		return 1
	elif n <= 65_536
		return 2
	else
		return 3

fun Buffer_auto_grown_size cap:Uint atleast:Uint = ret:Uint
	--post ret >= atleast
	if cap >= atleast
		return cap

	let shift = growth_factor_shift cap
	let min_newcap = cap + (cap >> shift)
	if atleast < min_newcap
		return min_newcap
	else
		return atleast

fun Buffer_auto_shrunk_size cap:Uint count:Uint = ret:Uint
	--pre count <= cap
	--post ret >= count
	if cap <= 16
		return cap

	let shift = growth_factor_shift count
	let max_newcap = cap - (cap >> (shift+1))
	if count <= max_newcap
		return max_newcap
	else
		return cap

example -> assert 16 == Buffer_auto_shrunk_size 16 0
example -> assert 9 == Buffer_auto_shrunk_size 17 0
example -> assert 128 == Buffer_auto_grown_size 128 127
example -> assert 128 == Buffer_auto_grown_size 128 128
example -> assert 256 == Buffer_auto_grown_size 128 129
example -> assert 256 == Buffer_auto_shrunk_size 256 255
example -> assert 256 == Buffer_auto_shrunk_size 256 129
example -> assert 128 == Buffer_auto_shrunk_size 256 128
example -> assert 256 == Buffer_auto_grown_size 256 155
example -> assert 256 == Buffer_auto_grown_size 256 256
example -> assert 512 == Buffer_auto_grown_size 256 257
example -> assert 385 == Buffer_auto_grown_size 257 258

inline Buf met# Reserve atleast:Uint
	if self.s.cap >= atleast
		return
	Realloc #self#s atleast

Buf met# auto_grow atleast:Uint
	let cap = self.s.cap
	if cap >= atleast
		return
	let newcap = Buffer_auto_grown_size cap atleast
	Realloc #self#s newcap

Buf met# auto_shrink
	let cap = self.s.cap
	let newcap = Buffer_auto_shrunk_size cap self.s.cnt
	if newcap == cap
		return
	Realloc #self#s newcap

example
	let v = {}:(Buf Uint) such
		assert v.Count == 0
		foreach i in 0 .. 63
			(v#Push)! = i
		assert v.Cap == 64
		v#Reserve 112
		assert v.Count == 63
		assert v.Cap == 112 -- Reserve allocates exactly what you ask.
		let r = v.Count .. v.Cap
		assert r.Count == 112-63
		foreach i in r
			(v#Push)! = i
			assert v.Cap == 112
		assert v.Count == 112
		assert v.Cap == 112
		(v#Push)! = 1
		assert v.Cap == 224
		foreach _ in 0 .. (v.Count - 113)
			v#Pop
		assert v.Count == 113
		assert v.Cap == 224
		v#Pop
		assert v.Count == 112
		assert v.Cap == 112

Buf met Show st:#`Fmt_state
	return self.s.Show st
