from nlang.prelude import *
from nlang.containers import *
from nlang.vectormod import *
from nlang.meta import *

-- vararg is an iterator type, not a container.
--
-- It can only be used in the function that created it, as it is only a
-- thin wrapper around C99's stdarg.h va_{start,arg,end,copy} family of
-- macros.
--
-- va_start is called by the compiler as first statement in the function.
-- As a result, the (vararg t) can only be iterated over a single time.
-- There is no easy way for us to reset the va_list with a call to va_copy and
-- va_start.
-- For that, we would have to rewrite calls to (vararg r).reset in the compiler.
-- We could add support for that at a later stage.
--
-- At this point, to iterate over the arguments several time, use append_to
-- with an auxiliary vector.

extern type __va_list =

(type t) vararg_iterator r:(any_ref t) = (iterator r)
  _v:@!(vararg r)

  method iterator.get = @#r
    return @#self._v!_current

  method iterator.has_next = bool
    return self._v._left > 0

  -- Requires compiler support, using the macro NLANG_VARARGMOD_VARARG_ITERATOR_NEXT().
  extern method! iterator.next = bool

-- Not copyable even though it is "passed" by-value in the prototype of a vararg
-- function. This is allowed by special case in the compiler.
--
(type t) vararg r:(any_ref t) = (iterable r)
  _ap:__va_list
  _left:u32
  _current:r
  _reset_count:u32

  let locus:typealias = r
  let iterator:typealias = vararg_iterator r

  method iterable.at p:@r = @r
    return p

  method! iterable.hg_at p:@#r = @#r
    return p

  method iterable.iter = this.iterator
    return this.iterator{{ _v=(nlang.unsafe.mutable_cast self) }}

  method! append_to v:@!(vector r) = void
    for a in self.iter
      v!push (self.at a).
